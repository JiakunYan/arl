cmake_minimum_required(VERSION 3.13)
project(ARL VERSION 0.5.0)
set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)
enable_testing()

set(ARL_BACKEND gex CACHE STRING "Backend")
set_property(CACHE ARL_BACKEND PROPERTY STRINGS gex lci)
if(ARL_BACKEND STREQUAL "gex")
    set(ARL_USE_GEX ON)
elseif(ARL_BACKEND STREQUAL "lci")
    set(ARL_USE_LCI ON)
else()
    message(FATAL_ERROR "Fabric ${LCI_SERVER} not supported")
endif()

option(ARL_DEBUG "Enable debug mode" OFF)
option(ARL_INFO "Enable info mode" OFF)

LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake_modules")
include(ARLUtils)

add_library(arl INTERFACE)
target_include_directories(arl INTERFACE arl)
configure_file(arl/config.hpp.in arl/config.hpp @ONLY)
target_include_directories(arl INTERFACE ${CMAKE_BINARY_DIR}/arl)

if(ARL_USE_GEX)
    find_package(PkgConfig REQUIRED)
    pkg_check_modules(gasnet QUIET IMPORTED_TARGET gasnet-aries-par)
    if(NOT gasnet_FOUND)
        pkg_check_modules(gasnet REQUIRED IMPORTED_TARGET gasnet-mpi-par)
        find_package(MPI REQUIRED)
        set(NEED_MPI ON)
    endif()
    # fix a cmake bug when encountering multiple "--param <val>" options
    string(REPLACE "--param;" "SHELL:--param " gasnet_CFLAGS_OTHER_FIX "${gasnet_CFLAGS_OTHER}")
    set_target_properties(PkgConfig::gasnet PROPERTIES INTERFACE_COMPILE_OPTIONS "${gasnet_CFLAGS_OTHER_FIX}")
    string(REPLACE "--param;" "SHELL:--param " gasnet_LDFLAGS_OTHER_FIX "${gasnet_LDFLAGS_OTHER}")
    set_target_properties(PkgConfig::gasnet PROPERTIES INTERFACE_LINK_OPTIONS "${gasnet_LDFLAGS_OTHER_FIX}")
    # Since GASNet is a static library, the order of linkage matters.
    # You need to first link GASNet and then MPI, not the reverse.
    target_link_libraries(arl INTERFACE PkgConfig::gasnet)
    if(NEED_MPI)
        target_link_libraries(arl INTERFACE MPI::MPI_C)
    endif()
elseif(ARL_USE_LCI)
    find_package(LCI REQUIRED)
    find_package(MPI REQUIRED)
    find_package(Threads REQUIRED)
    target_link_libraries(arl INTERFACE LCI::Shared MPI::MPI_C Threads::Threads)
endif()

add_subdirectory(examples)
add_subdirectory(tests)